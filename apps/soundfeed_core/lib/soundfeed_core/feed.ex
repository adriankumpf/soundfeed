defmodule SoundfeedCore.Feed do

  alias SoundfeedCore.Models.{Track, User}
  alias SoundfeedCore.Feed.{RSS, Helper}

  @desc """
  Generated by SoundFeed:
  https://github.com/adriankumpf/soundfeed
  """

  def build(tracks, type, %User{id: user_id, username: user_name, permalink_url: url}) do
    title = "#{user_name} - #{Atom.to_string(type)}"
    now = Helper.now_rfc1123()

    channel = RSS.channel(title, url, @desc, now)
    items = Enum.map(tracks, &create_item(&1, user_id))

    RSS.feed(channel, items)
  end

  defp create_item(%Track{title: title, description: desc, permalink_url: url}, user_id) do
    guid = url <> "?user=#{user_id}"
    RSS.item(title, desc, url, guid)
  end
end
